import baseos "./baseos.hlb"

import hlb "./hlb.hlb"

import nodejs "./nodejs.hlb"

import project "./project.hlb"

import titus from fs {
	image "dockerregistry.test.netflix.net:7002/mart/titus.hlb"
}

fs build() {
	baseos.build
	copy nodejs.build "/" "/" with createDestPath
	project.build
	copy hlb.build "hlb-linux-amd64" "/bin/hlb"
}

fs publish() {
	build
	titus.registryPush string {
		format "mart/mass:%s" string { localRun "date +%s"; }
	}
	titus.registryPush "mart/mass:latest"
}

fs rocketCI() {
	build
	copy fs {
		local "../" with option {
			includePatterns "scripts" "tests"
		}
	} "/" "/opt/mass"
	run <<~APT
		apt-get update &&
		apt-get install --no-install-recommends -y shellcheck &&
		apt-get clean &&
		rm -rfv /var/lib/apt/lists/* /var/cache/apt/*
	APT
	copy npmInstall "/" "/opt/mass/node_modules"
	dockerLoad "mass-test-environment"
}

fs npmci() {
	baseos.build
	copy nodejs.build "/" "/" with createDestPath
	run <<~APT
		apt-get update &&
		apt-get install --no-install-recommends -y make python gcc g++ &&
		apt-get clean &&
		rm -rfv /var/lib/apt/lists/* /var/cache/apt/*
	APT
	run "npx node-gyp install"
	run "npm ci" with option {
		mount fs {
			local "../" with option {
				includePatterns "package.json" "package-lock.json" ".npmrc"
			}
		} "/in" with readonly
		mount scratch "/in/node_modules" as npmInstall
		dir "/in"
	}
}
